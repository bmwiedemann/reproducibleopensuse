#!/usr/bin/perl -w
use strict;
use XML::Simple;
use JSON;
use POSIX;

sub find_version($$)
{
  my $dir=shift;
  my $name=shift;
  my $version=`rpmspec --query --srpm --queryformat="%{version}" $name.spec 2>/dev/null`;
  if($version eq "") { # could not parse .spec file
    $version=`grep '^Version:' $name.spec|head -1`;
    chomp($version);
    $version=~s/^Version:\s*//;
    if($version =~ /%/) {
      my $line=`grep "Wrote: .*\.src\.rpm" $dir/.build.log`;
      if($line) {
        $line =~ /.*-([^-]+)-[^-]+\.src\.rpm/ && return $1;
      }
      warn "cannot find version for package $name - got $version";
      return "unknown";
    }
  }
  return $version;
}

sub getline($)
{  my $file=shift;
   open(my $f, "<", $file) or die "error opening $file : $!";
   my $line=<$f>;
   chomp($line);
   return $line;
}

sub find_status($)
{ my $dir=shift;
  my $build=getline("$dir/.osc-build-retval");
  if($build == 2) { return "notforus" }
  if($build == 1) {
    #TODO: check .build.log for arch mismatch/unresolvable
    system(qw(grep -q), "error: Architecture is not included", "$dir/.build.log");
    if(($?>>8) == 0) { return "notforus" }
    system(qw(grep -q), "^unresolvable: nothing provides", "$dir/RPMS/.build.log2");
    if(($?>>8) == 0) { return "waitdep" }
    return "FTBFS";
  }
  if($build eq "0") {
    my $differed=eval{getline("$dir/.build-differed")};
    if($@) { return "nobinaries" } # i.e. testsuite
    if($differed eq "0") { return "reproducible" }
    return "unreproducible";
  }
  return "unknown";
}

sub get_status_for_one_dir($)
{
  my $dir=shift;
  my $oscdata=XMLin("$dir/.osc/_files");
  my $name=$oscdata->{name};
  my $version=find_version($dir, "$dir/$name");
  return {
    package=>$name,
    version=>"$version-$oscdata->{rev}",
    architecture=>(POSIX::uname())[4],
    status=>find_status($dir),
    suite=>getline("$dir/.osc/_project"),
    build_date=>(stat("$dir/.osc-build-retval"))[9],
  };
}

my $result=[];
for my $dir (@ARGV) {
  push(@$result, get_status_for_one_dir($dir));
}
print JSON->new->ascii->pretty->allow_nonref->canonical->encode($result);
