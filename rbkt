#!/usr/bin/perl -w
# build one package with reduced indeterminism
# returns 0 for a reproducible build
# returns 1 for an unreproducible build
# returns 255 for a failed build
# usage:
#  CLEAN=1 rbkt 0 0 1 0 0 0 # to build with sorted filesystem
#  CLEAN=1 rbkt 1 1 1 1 1 1 1 1 1 1 # to build with as little indeterminism as possible
#  rbkt 12 is decoded as hex for 0 1 0 0 1


use strict;
sub rbk(@)
{
    my @tweakbits=@_;
    my @extrapkg=();
    if($ENV{CLEAN} || $_[2]) { $ENV{clean}="--clean --offline"} else { $ENV{clean}="--offline" }
    # bit 1
    if(shift) { $ENV{datediff}=100 }
    # bit 2
    my $hosttweak=shift||0;
    if($hosttweak&1) { $ENV{host2}="buildhosta" }
    if($hosttweak&2) { push(@extrapkg, "reproducible-faketools-hostname") }
    # bit 3
    my $fstweak=shift||0;
    if($fstweak&1) { push(@extrapkg, "reproducible-faketools-filesys"); $ENV{rbfilesys}="reproducible" }
    if($fstweak&2) { $ENV{rbfilesys}="reproducible" }
    # bit 4
    if(shift) { push(@extrapkg, "reproducible-faketools-date") }
    # bit 5
    if(shift) { $ENV{rbsnd}=1 } # use strip-nondeterminism
    # bit 6
    if(shift) { $ENV{project}="home:bmwiedemann:reproducible:test" }
    # bit 7
    if(shift) { push(@extrapkg, "reproducible-faketools-tar") }
    # bit 8
    if(shift) { $ENV{parallelism}=1; $ENV{parallelism2}=1 }
    # bit 9
    if(shift) { push(@extrapkg, "reproducible-faketools-aslr") }
    # bit 10
    if(shift) { push(@extrapkg, "reproducible-faketools-pid") }
    # bit 11
    if(shift) { push(@extrapkg, "reproducible-faketools-random") }
    # bit 12
    if(shift) { $ENV{cputype2}="host" }
    # bit 13 and beyond are unreliable and thus not used in autoclassify
    if(shift) { push(@extrapkg, "reproducible-faketools-faketime") }
    if(shift) { push(@extrapkg, "reproducible-faketools-strace") } # get extra r-b debug info
    if(shift) { push(@extrapkg, "reproducible-faketools-dettrace") }
    if(@extrapkg) { $ENV{oscbuildextrapkg}=join " ", map {"--extra-pkgs=$_"} @extrapkg }
    system("rbk");
    system("echo @tweakbits > .build-rbkt");
    system("jsonresultcache ."); # update with tweakbits
    my $resultfile=$ENV{rbminor}?".build-compare-retval":".build-differed";
    open(my $resultfd, "<", $resultfile) || die $!;
    my $result=<$resultfd>;chomp($result);
    if($result>1) {$result=1}
    return $result;
}

sub run(@)
{
    if(@_==1) { # decode hex
         @_=reverse(split("", sprintf("%b", hex(shift))));
    }
    #print "trying @_\n";
    rbk(@_);
}

exit run(@ARGV);
